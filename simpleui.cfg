//UISimple Elements
//Templates To Make Menus Quicker
//Author: Dale Weiler

UISimpleHover =    [ uimodcolor 1 .75 .75 0 0 [uiclamp 1 1 1 1]]
UISimpleSelected = [ uimodcolor .75 .75 1 0 0 [uiclamp 1 1 1 1]]
UISimpleDisabled = [ uimodcolor .2 .2 .2 0 0  [uiclamp 1 1 1 1]]

//Close Button - Menu Element
//Usage Example: UICloseButton [hideui "main"]
//
UISimpleCloseButton = [
	uibutton $arg1 [
		uialign 1 -1
		uiimage "data/newui/exit.jpg" 0.04 0.04
		uiimage "data/newui/exit.jpg" 0.04 0.04 UISimpleHover
		uiimage "data/newui/exit.jpg" 0.04 0.04 UISimpleSelected
	]
]

//Simple Button - Menu Element
//Usage Example: UISimpleButton "Close Game" [quit]
//
UISimpleButton = [
	uibutton $arg2 [
		uifill .35 .05 [
			uiclamp 1 1 0 0
			uihlist 0 [
				uistretchedimage "data/newui/button_idle_left" .05 .05
				uistretchedimage "data/newui/button_idle_body" 0.25 0.05 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/button_idle_right" .05 .05
			]
			uitext [@@@arg1] 1.5
		]
		uifill .35 .05 [
			uiclamp 1 1 0 0
			uihlist 0 [
				uistretchedimage "data/newui/button_hover_left" .05 .05
				uistretchedimage "data/newui/button_hover_body" 0.25 0.05 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/button_hover_right" .05 .05
			]
			uitext [@@@arg1] 1.5
		]
		uifill .35 .05 [
			uiclamp 1 1 0 0
			uihlist 0 [
				uistretchedimage "data/newui/button_selected_left" .05 .05
				uistretchedimage "data/newui/button_selected_body" 0.25 0.05 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/button_selected_right" .05 .05
			]
			uitext [@@@arg1] 1.5
		]
	]
]

//Simple Show - Menu Element
//Usage Example: UISimpleShow "menuname"
//
UISimpleShow = [
	(format "show%1" $arg1)
]

//Simple Hide - Menu Element
//Usage Example: UISimpleHide "menuname"
//
UISimpleHide = [
	hideui $arg1
]

//Simple VList - Menu Element
//Usage Example: (N/A)
//
UISimpleVList = [
	uivlist 0.02 [
		@arg1
	]
]

//Simple Icon - Menu Element
//Usage Example UISimpleIcon "icon.png"
//
UISimpleIcon = [
	uiimage (format "data/newui/icons/%1" $arg1) .05
]

//Simple HList - Menu Element
//Usage Example: (N/A)
//
UISimpleHList = [
	uihlist 0.02 [
		@arg1
	]
]

//Simple Text - Menu Element
//Usage Example: UISimpleText "text"
//
UISimpleText = [
	uitext $arg1 1.5
]

//Simple ScrollBox - Menu Element
//Usage Example: UISimpleScrollBox 100 200 othergui
//
UISimpleScrollBox = [
	uihlist 0 [
		uiscroll (-f @arg1 .03) @arg2 [
			uifill (-f @@arg1 .03) @@arg2
			@@arg3
		]
		uivscrollbar 0.03 1 [
			uifill 0.03 0 [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_idle_up" .03 .03
					uistretchedimage "data/newui/bar_vertical" .03 (-f @@@@arg2 .06)
					uistretchedimage "data/newui/arrow_idle_down" .03 .03
				]
			]
			uifill 0.03 0 [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_hover_up" .03 .03
					uistretchedimage "data/newui/bar_vertical" .03 (-f @@@@arg2 .06)
					uistretchedimage "data/newui/arrow_idle_down" .03 .03
				]
			]
			uifill 0.03 0 [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_selected_up" .03 .03
					uistretchedimage "data/newui/bar_vertical" .03 (-f @@@@arg2 .06)
					uistretchedimage "data/newui/arrow_idle_down" .03 .03
				]
			]
			uifill 0.03 0 [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_idle_up" .03 .03
					uistretchedimage "data/newui/bar_vertical" .03 (-f @@@@arg2 .06)
					uistretchedimage "data/newui/arrow_hover_down" .03 .03
				]
			]
			uifill 0.03 0 [
				uivlist 0 [
					uistretchedimage "data/newui/arrow_idle_up" .03 .03
					uistretchedimage "data/newui/bar_vertical" .03 (-f @@@@arg2 .06)
					uistretchedimage "data/newui/arrow_selected_down" .03 .03
				]
			]

			uiscrollbutton [
				uistretchedimage "data/newui/scrollbutton_vertical_idle" .03 .03 [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/scrollbutton_vertical_hover" .03 .03 [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/scrollbutton_vertical_selected" .03 .03 [uiclamp 0 0 1 1]
			]
		]
	]
]

//Simple HeadLine - Menu Element
//Usage Example: UISimpleHeadLine 1 "this is a headline size 1"
//
UISimpleHeadLine = [
	if ( = $arg1 1) [uitext $arg2 1.60] [
	if ( = $arg1 2) [uitext $arg2 1.70] [
	if ( = $arg1 3) [uitext $arg2 1.80] [
	if ( = $arg1 4) [uitext $arg2 1.90] [
	if ( = $arg1 5) [uitext $arg2 2.00] [
	]]]]]
]

//Simple QuoteText - Menu Element
//Usage Example: UISimpleQuoteText "this text will be quoted inside the menu"
//
UISimpleQuoteText = [
	uitext (format "^"%1^"" $arg1) 0.75
]

//Simple Checkbox - Menu Element
//Usage Example: UISimpleCheckBox [arg to toggle 0-1] Description
//
UISimpleCheckBox = [
	uihlist 0 [
		uialign -1 -1
		uitoggle [ $@@arg1 ] [ @@arg1 (! $@@arg1) ] 0 [
			uistretchedimage "data/newui/toggle_false_idle" .05 .05
			uistretchedimage "data/newui/toggle_false_hover" .05 .05
			uistretchedimage "data/newui/toggle_true_idle" .05 .05 [ uitext "X" 1.75 ]
			uistretchedimage "data/newui/toggle_true_hover" .05 .05 [ uitext "X" 1.75 ]
		]
		uioffset 0.02 0 [
			uitext [@@arg2] 1.5
		]
	]
]

//Simple ColorText - Menu Element
//Usage Example: UISimpleColorText "this text will be red" Red
//
UISimpleColorText = [
	//The standard 16 Web colors in order
	if (strcmp $arg2 "black" ) [uicolortext $arg1 1.5 1.00 1.00 1.00] [
	if (strcmp $arg2 "gray"  ) [uicolortext $arg1 1.5 1.28 1.28 1.28] [
	if (strcmp $arg2 "silver") [uicolortext $arg1 1.5 1.92 1.92 1.92] [
	if (strcmp $arg2 "white" ) [uicolortext $arg1 1.5 2.55 2.55 2.55] [
	if (strcmp $arg2 "maroon") [uicolortext $arg1 1.5 1.28 0.00 0.00] [
	if (strcmp $arg2 "red"   ) [uicolortext $arg1 1.5 2.55 0.00 0.00] [
	if (strcmp $arg2 "purple") [uicolortext $arg1 1.5 1.28 0.00 1.28] [
	if (strcmp $arg2 "pink"  ) [uicolortext $arg1 1.5 2.49 1.32 2.29] [
	if (strcmp $arg2 "green" ) [uicolortext $arg1 1.5 0.00 1.59 1.07] [
	if (strcmp $arg2 "lime"  ) [uicolortext $arg1 1.5 1.91 2.55 0.00] [
	if (strcmp $arg2 "olive" ) [uicolortext $arg1 1.5 1.28 1.28 0.00] [
	if (strcmp $arg2 "yellow") [uicolortext $arg1 1.5 2.55 2.05 0.00] [
	if (strcmp $arg2 "navy"  ) [uicolortext $arg1 1.5 0.00 0.00 1.28] [
	if (strcmp $arg2 "blue"  ) [uicolortext $arg1 1.5 0.00 0.00 2.55] [
	if (strcmp $arg2 "teal"  ) [uicolortext $arg1 1.5 0.00 1.28 1.28] [
	if (strcmp $arg2 "aqua"  ) [uicolortext $arg1 1.5 0.00 2.55 2.55] [
	]]]]]]]]]]]]]]]]
]

//Simple Menu - Menu Element
//Usage Example: UISimpleMenu "Menu Name" [...contents] "Initial Title"
//
UISimpleMenu = [
	(format "show%1" $arg1) = [
		showui @arg1 [
			uitable 3 0 [
				uistretchedimage "data/newui/corner_top_left" 0.04 0.04
				uistretchedimage "data/newui/edge_top" 0 .04 [
					uiclamp 1 1 0 0
					uitag title [
						uialign 0 0
						uitext [@@@@@@arg3] 1.5
					]
					uibutton [ hideui @@@@@arg1 ] [
						uialign 1 0
						uiimage "data/hud/2.1/icons/exit.jpg" 0.04 0.04
						uiimage "data/hud/2.1/icons/exit.jpg" 0.04 0.04 UISimpleHover
						uiimage "data/hud/2.1/icons/exit.jpg" 0.04 0.04 UISimpleSelected
					]
				]
				uistretchedimage "data/newui/corner_top_right" 0.04 0.04

				uistretchedimage "data/newui/edge_left" .04 0 [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/body" 0 0 [uiclamp 1 1 1 1; @@@@arg2]
				uistretchedimage "data/newui/edge_right" .04 0 [uiclamp 0 0 1 1]

				uistretchedimage "data/newui/corner_bottom_left" .04 0.04
				uistretchedimage "data/newui/edge_bottom" 0 0.04 [uiclamp 1 1]
				uistretchedimage "data/newui/corner_bottom_right" .04 0.04
			]
		]
	]
]

//Simple Menu With No Close Button - Menu Element
//Usage Example: UISimpleMenuNoClose "Menu Name" [...contents] "Initial title"
//

UISimpleMenuNoClose = [
	(format "show%1" $arg1) = [
		showui @arg1 [
			uitable 3 0 [
				uistretchedimage "data/newui/corner_top_left" 0.04 0.04
				uistretchedimage "data/newui/edge_top" 0 .04 [
					uiclamp 1 1 0 0
					uitag title [
						uialign 0 0
						uitext [@@@@@@arg3] 1.5
					]
				]
				uistretchedimage "data/newui/corner_top_right" 0.04 0.04

				uistretchedimage "data/newui/edge_left" .04 0 [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/body" 0 0 [uiclamp 1 1 1 1; @@@@arg2]
				uistretchedimage "data/newui/edge_right" .04 0 [uiclamp 0 0 1 1]

				uistretchedimage "data/newui/corner_bottom_left" .04 0.04
				uistretchedimage "data/newui/edge_bottom" 0 0.04 [uiclamp 1 1]
				uistretchedimage "data/newui/corner_bottom_right" .04 0.04
			]
		]
	]
]

//Simple Menu With No Tile - Menu Element
//Usage Example: UISimpleMenuNoTitle "Menu Name" [...contents]
//
UISimpleMenuNoTitle = [
	(format "show%1" $arg1) = [
		showui @arg1 [
			uicolor .6 .8 1 .8 0 0 [
				@@@arg2
			]
		]
	]
]

UISimpleMenuNoTitle = [
	(format "show%1" $arg1) = [
		showui @arg1 [
			uitable 3 0 [
				uistretchedimage "data/newui/corner_top_left" 0.04 0.04
				uistretchedimage "data/newui/edge_top" 0 .04 [uiclamp 1 1 0 0]
				uistretchedimage "data/newui/corner_top_right" 0.04 0.04

				uistretchedimage "data/newui/edge_left" .04 0 [uiclamp 0 0 1 1]
				uistretchedimage "data/newui/body" 0 0 [uiclamp 1 1 1 1; @@@@arg2]
				uistretchedimage "data/newui/edge_right" .04 0 [uiclamp 0 0 1 1]

				uistretchedimage "data/newui/corner_bottom_left" .04 0.04
				uistretchedimage "data/newui/edge_bottom" 0 0.04 [uiclamp 1 1]
				uistretchedimage "data/newui/corner_bottom_right" .04 0.04
			]
		]
	]
]