//TODO
//////
//possibility to run quest with different person with different values the same time
//make quest not restart infinetely with the same NPC


//get villagers food
r_script_signal_append "spawn" [
	r_local_set foodq_state 0
	r_signal "restartfoodquest" self self 0
]


r_script_signal "restartfoodquest" [
	local fooditems idx
	fooditems = [
		[ 0    @@(rnd 10 5) ]  //apples
		[ 14   @@(rnd 4 2)  ]  //bread
		[ 20   @@(rnd 6 2)  ]  //cooked meat
		[ 21   @@(rnd 10 8) ]  //pears
		[ 41   @@(rnd 18 8) ]  //billberries
	]

	idx = (rnd (listlen $fooditems))

	r_local_set self foodq_item (at $fooditems $idx 0)
	r_local_set self foodq_amnt (at $fooditems $idx 1)
]

r_script_node "getfood" [
	r_stack [
		r_local item
		r_setref_inv item player (r_local_get talker foodq_item)
		r_select_item item [
			format "Could you please get me %1 %2?" (r_local_get talker foodq_amnt) $r_item_name
		]
	]
] [
	local base qty
	base = (r_local_get talker foodq_item)
	qty = (r_local_get talker foodq_amnt)
	r_local item
	r_setref_invstack item player $base
	if (! (r_ref_len $item)) [ r_setref_inv item player $base ]

	r_select_item item [
		r_journal_record "Get Food" (format "%1 has requested that I bring him %2 %3(s)" (r_get_name talker) $qty $r_item_name)

		if (>= (r_get_amount player $base) $qty) [
			r_response "I happen to have that here, here you go." "foodreward" [
				r_transfer player talker item $qty
			]
		]
	]
	r_response "I'll return soon with the items." ""
	r_response "No, get them yourself." ""
]

r_script_node "foodreward" [
	result "You have my thanks, please, take this in return for your efforts."
] [
	r_response "Keep it, you have greater need of it than I." "foodthankyou"

	r_response "Pleasure doing business, farewell." "" [
		local base qty reward
		r_local item

		base = (r_local_get talker foodq_item)
		qty = (r_local_get talker foodq_amnt)

		r_setref_inv item player $base
		r_select_item item [
			reward = (+ (*f $r_item_value $qty) (min (- (r_get_relationship talker player) 50) 8))
			reward = (max 0 $reward)
		]

		r_setref item talker 16 //get his coins

		r_mod_relationship talker player 2
		r_transfer talker player item $reward
		r_givexp player (* 2 $reward)

		r_signal "restartfoodquest" self self 0
		r_journal_record "Get Food" (format "I've returned to %1 with the items, he gave me %2 coppers." (r_get_name talker) $reward)
	]
]

r_script_node "foodthankyou" [result "Thank you very much!"] [
	r_response "No problem, just happy to help." "" [
		local base qty reward
		r_local item

		base = (r_local_get talker foodq_item)
		qty = (r_local_get talker foodq_amnt)

		r_setref_inv item player $base
		r_select_item item [
			reward = (+ (*f $r_item_value $qty) (min (- (r_get_relationship talker player) 50) 8))
			reward = (max 0 $reward)
		]

		r_mod_relationship talker player 10
		r_givexp player (* 3 $reward)

		r_signal "restartfoodquest" self self 0
		r_journal_record "Get Food" (format "I've returned to %1 with the items and declined the reward" (r_get_name talker))
	]

	r_response "No, seriously. Give me the reward." "foodreward"
]
